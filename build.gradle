plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'org.beryx.jlink' version '2.26.0'
}

group = 'Tanks'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://repo1.maven.org/maven2/"
    }
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
            '--add-reads', 'tanks.game=ALL-UNNAMED'
    ]
}

application {
    mainClass = 'Tanks.App'
    mainModule = 'tanks.game'
}

run {
    jvmArgs = [
            '--add-reads', 'tanks.game=ALL-UNNAMED',
            '--add-opens', 'tanks.game/Tanks=ALL-UNNAMED',
            '--add-exports', 'java.desktop/sun.java2d=ALL-UNNAMED'
    ]
}

dependencies {
    implementation 'org.processing:core:3.3.7'
    implementation 'org.checkerframework:checker-qual:3.32.0'
    testImplementation 'junit:junit:4.13.2'
}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Tanks'
        noConsole = true
        jvmArgs = [
                '--add-reads', 'tanks.game=ALL-UNNAMED',
                '--add-opens', 'tanks.game/Tanks=ALL-UNNAMED',
                '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
                '--add-opens', 'java.base/java.io=ALL-UNNAMED',
                '--add-exports', 'java.desktop/sun.java2d=ALL-UNNAMED',
                '-Dfile.encoding=UTF-8'
        ]
    }

    forceMerge('org.processing.core', 'org.checkerframework.checker.qual')
    addOptions('--verbose')

    mergedModule {
        requires('java.desktop')
        additive = true
        uses('processing.core.PApplet')
    }

    jpackage {
        appVersion = '1.0.0'
        outputDir = "installers"
        imageName = "Tanks"
        skipInstaller = false

        installerName = 'Tanks'
        installerType = 'exe'

        resourceDir = file("src/main/resources")

        installerOptions = [
                '--win-menu',
                '--win-dir-chooser',
                '--win-shortcut',
                '--win-per-user-install',
                '--win-menu-group', 'Tanks',
                '--description', 'Tanks Game',
                '--verbose'
        ]
    }
}

tasks.named('jpackage').configure {
    doFirst {
        copy {
            from 'src/main/resources'
            into 'build/jpackage/Tanks/app'
            include '**/*'
        }
    }
}